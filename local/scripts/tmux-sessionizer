#!/usr/bin/env fish

set -l search_roots ~/personal ~/work ~/rebelscan ~/notes
set -l roots

for root in $search_roots
  if test -d $root
    set -a roots $root
  end
end

if not set -q roots[1]
  echo "tmux-sessionizer: no search roots found" >&2
  exit 1
end

for cmd in fd fzf tmux
  if not type -q $cmd
    echo "tmux-sessionizer: missing dependency: $cmd" >&2
    exit 1
  end
end

set -l dirs
for root in $roots
  set -a dirs (fd --type d --hidden --exclude .git --absolute-path --max-depth 1 --min-depth 1 . $root)
end

if test (count $dirs) -eq 0
  echo "tmux-sessionizer: no directories found" >&2
  exit 1
end

set -l selection (printf "%s\n" $dirs | fzf)
if test -z "$selection"
  exit 0
end

set -l name (basename "$selection")
set -l name (string replace -ar '[^A-Za-z0-9_-]' '_' $name)
set -l name (string replace -ar '_+' '_' $name)
if test -z "$name"
  set name session
end

if tmux has-session -t "$name" 2>/dev/null
  if set -q TMUX
    tmux switch-client -t "$name"
  else
    tmux attach-session -t "$name"
  end
else
  tmux new-session -d -s "$name" -c "$selection"
  if set -q TMUX
    tmux switch-client -t "$name"
  else
    tmux attach-session -t "$name"
  end
end
